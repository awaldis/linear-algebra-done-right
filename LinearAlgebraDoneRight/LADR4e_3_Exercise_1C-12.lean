import Mathlib.Algebra.Module.Basic
import Mathlib.Algebra.Field.Basic
import Mathlib.Algebra.Module.Submodule.Basic

/-!
# Exercise 1C.12 - Prove that the union of two subspaces of ğ‘‰ is a subspace
of ğ‘‰ if and only if one of the subspaces is contained in the other.
## From:
Sheldon Axler. [Linear Algebra Done Right](https://linear.axler.net), fourth
edition, Undergraduate Texts in Mathematics, Springer, 2024
-/
variable {ğ”½ : Type*} [Field ğ”½]
variable {V : Type*} [AddCommGroup V] [Module ğ”½ V]

theorem union_is_subspace_iff (U W : Submodule ğ”½ V) :
  (âˆƒ S : Submodule ğ”½ V, (S : Set V) = (U : Set V) âˆª (W : Set V)) â†”
                                                 (U â‰¤ W âˆ¨ W â‰¤ U) := by
  constructor
  Â· -- FORWARD DIRECTION (â‡’): If the union is a subspace, U âŠ† W or W âŠ† U.
    intro âŸ¨S, h_S_is_union_of_U_and_WâŸ©

    -- We use classical logic: Either U â‰¤ W is true, or it is false.
    by_cases hUW : U â‰¤ W
    Â· -- If U â‰¤ W is true, then the left side is proven immediately.
      exact Or.inl (hUW : U â‰¤ W)
    Â· -- If U â‰¤ W is false, then we must prove W â‰¤ U.
      apply Or.inr
      -- New goal: W â‰¤ U

      intro w (hw : w âˆˆ W)
      -- New goal:  w âˆˆ U

      -- In this branch we have Â¬(U â‰¤ W), so there must exist some u âˆˆ U such
      -- that u âˆ‰ W.  Let's prove it so we can use it.
      have h_at_least_one_u_not_in_W : âˆƒ u, u âˆˆ U âˆ§ u âˆ‰ W := by
        -- Assume the opposite of the goal and show that it leads to a
        -- contradiction.
        by_contra h_no_u_not_in_W
        -- New goal: False
        -- Change the Â¬âˆƒ to âˆ€
        push_neg at h_no_u_not_in_W
        apply (hUW : Â¬U â‰¤ W)
        -- New Goal: U â‰¤ W
        intro x (hx : x âˆˆ U)
        -- New goal: x âˆˆ W
        exact h_no_u_not_in_W x (hx : x âˆˆ U)

      -- Let's extract that specific element 'u'
      rcases h_at_least_one_u_not_in_W with âŸ¨u, h_u_in_U, h_u_not_in_WâŸ©

      -- Since u is in U, we can obtain a proof that u is in S.
      have h_u_in_S : u âˆˆ S := by
        -- Explicitly expose the set coercion so `rw` can find â†‘S
        change u âˆˆ (S : Set V)
        rw [h_S_is_union_of_U_and_W, Set.mem_union]
        exact Or.inl h_u_in_U

      -- Since w is in W, we can obtain a proof that w is in S.
      have h_w_in_S : w âˆˆ S := by
        change w âˆˆ (S : Set V)
        rw [h_S_is_union_of_U_and_W, Set.mem_union]
        exact Or.inr hw

      -- Since S is a subspace, it is closed under addition.  So we can obtain
      -- a proof that the sum of two of it's members is also a member.
      have h_uw_sum_in_S : u + w âˆˆ S := S.add_mem h_u_in_S h_w_in_S

      -- Since S = U âˆª W, the sum u + w must be in U or in W.
      have h_uw_sum_in_union : u + w âˆˆ (U : Set V) âˆª (W : Set V) := by
        rw [â† h_S_is_union_of_U_and_W]
        exact h_uw_sum_in_S

      -- Explicitly state the union as an 'Or' statement
      change (u + w âˆˆ U) âˆ¨ (u + w âˆˆ W) at h_uw_sum_in_union

      -- Obtain a proof that (u + w) cannot be in W
      have h_uw_sum_not_in_W : u + w âˆ‰ W := by
        -- In Lean, 'âˆ‰' means 'implies False'. So we assume it is in W.
        intro (h_uw_sum_in_W : u + w âˆˆ W)
        --New goal: False

        -- If (u + w) âˆˆ W, then u = (u + w) - w must be in W.
        have hu_eq : u = (u + w) - w := by simp
        have h_u_in_W : u âˆˆ W := by
          rw [hu_eq]  -- New goal: u + w - w âˆˆ W
          exact W.sub_mem (h_uw_sum_in_W : u + w âˆˆ W) (hw : w âˆˆ W)

        -- This contradicts our earlier fact that u âˆ‰ W.
        -- 'h_u_not_in_W' is a function that expects a proof of 'u âˆˆ W' to
        -- produce 'False'
        exact (h_u_not_in_W : u âˆ‰ W) (h_u_in_W : u âˆˆ W)

      -- Use Disjunctive Syllogism to conclude (u + w) âˆˆ U
      -- Since we have (a âˆ¨ b) and (Â¬b), Or.resolve_right gives us a.
      have h_uw_in_U : u + w âˆˆ U := Or.resolve_right
                            (h_uw_sum_in_union : u + w âˆˆ U âˆ¨ u + w âˆˆ W)
                            (h_uw_sum_not_in_W :             u + w âˆ‰ W)

      -- Now that we solidly have (u + w) âˆˆ U, we subtract u to get w âˆˆ U.
      have hw_eq : w = (u + w) - u := by simp
      -- Current goal: w âˆˆ U
      rw [hw_eq]
      -- New goal: u + w - u âˆˆ U
      exact U.sub_mem (h_uw_in_U : u + w âˆˆ U) (h_u_in_U : u âˆˆ U)
  -----------------------------------------------------------------------------
  Â· -- REVERSE DIRECTION (â‡): If U âŠ† W or W âŠ† U, the union is a subspace.
    -- This direction is pretty trivial since, on both sides of the OR, one
    -- subspace is completely contained by the other.  And we already know
    -- they are both subspaces.
    rintro ( (hUW : U â‰¤ W) | (hWU : W â‰¤ U) )
    Â· -- Case U âŠ† W: The union U âˆª W is just W.
      -- Goal: âˆƒ S, â†‘S = â†‘U âˆª â†‘W
      use W
      -- New goal: â†‘W = â†‘U âˆª â†‘W

      -- Set.union_eq_right.mpr proves (U âˆª W = W) given U âŠ† W.
      -- We add .symm to flip it to (W = U âˆª W) to match our exact goal.
      exact (Set.union_eq_right.mpr (hUW : U â‰¤ W)).symm

    Â· -- Case W âŠ† U: The union U âˆª W is just U.
      use U
      exact (Set.union_eq_left.mpr hWU).symm
