import Mathlib.Algebra.Field.Basic
import Mathlib.Algebra.Module.Submodule.Basic
import Mathlib.Tactic  -- Needed for "ring"

/-!
# Exercise 1C.1 - For each of the following subsets of ğ”½Â³, determine whether it
# is a subspaceof ğ”½Â³.
## From:
Sheldon Axler. [Linear Algebra Done Right](https://linear.axler.net), fourth
edition, Undergraduate Texts in Mathematics, Springer, 2024
-/
-- ğ”½ is a field of characteristic 0 (e.g., â„š, â„, â„‚).
-- This excludes finite fields like GF(2), GF(3), GF(p) and their extensions.
variable {ğ”½ : Type*} [Field ğ”½]
def Set_1a : Set (Fin 3 â†’ ğ”½) := {v | (v 0) + (2 * v 1) + (3 * v 2) = 0}

theorem axler_1C_1a : âˆƒ W : Submodule ğ”½ (Fin 3 â†’ ğ”½), (W : Set (Fin 3 â†’ ğ”½))
                                                                  = Set_1a := by
  use {
    carrier := Set_1a
    zero_mem' := by
      -- Goal: 0 âˆˆ Set_1a
      unfold Set_1a at *
      -- New goal: 0 âˆˆ {v | (v 0) + (2 * v 1) + (3 * v 2) = 0}
      simp only [Set.mem_setOf_eq] at *
      -- New goal: (0 0) + (2 * 0 1) + (3 * 0 2) = 0
      simp only [Pi.zero_apply]
      -- New goal: 0 + (2 * 0) + (3 * 0) = 0
      ring
    add_mem' {a b} ha hb := by
      -- Goal: a + b âˆˆ Set_1a
      unfold Set_1a at *
      -- New goal: a + b âˆˆ {v | (v 0) + (2 * v 1) + (3 * v 2) = 0}
      simp only [Set.mem_setOf_eq] at *
      -- New goal: (a + b) 0 + (2 * (a + b) 1) + (3 * (a + b) 2) = 0
      simp only [Pi.add_apply]
      -- New goal: (a 0 + b 0) + (2 * (a 1 + b 1)) + (3 * (a 2 + b 2)) = 0
      calc (a 0 + b 0) + (2 * (a 1 + b 1)) + (3 * (a 2 + b 2))
         =   (a 0 + (2 * a 1) + (3 * a 2))
           + (b 0 + (2 * b 1) + (3 * b 2)) := by ring
       _ = 0 + 0 := by rw [ha, hb]
       _ = 0 := by ring
    smul_mem' c v hv := by
       -- Goal: c â€¢ v âˆˆ Set_1a
       unfold Set_1a at *
       -- New goal: c â€¢ v âˆˆ {v | (v 0) + (2 * v 1) + (3 * v 2) = 0}
       simp only [Set.mem_setOf_eq] at *
       -- New goal: ((c â€¢ v) 0) + (2 * (c â€¢ v) 1) + (3 * (c â€¢ v) 2) = 0
       simp only [Pi.smul_apply]
       -- New goal: (c â€¢ v 0) + (2 * c â€¢ v 1) + (3 * c â€¢ v 2) = 0
       simp only [smul_eq_mul]
       -- New goal:
       --   (c * v 0) + (2 * (c * v 1)) + (3 * (c * v 2)) = 0
       calc (c * v 0) + (2 * (c * v 1)) + (3 * (c * v 2))
          = c * ((v 0) + (2 * v 1) + (3 * v 2)) := by ring
        _ = c * 0                               := by rw [hv]
        _ = 0                                   := by ring
  }
  rfl

--------------------------------------------------------------------------------
section CharZeroExercises
-- For these exercises we need to make ğ”½ a field of characteristic 0 (e.g., â„š, â„, â„‚).
-- This excludes finite fields like GF(2), GF(3), GF(p) and their extensions.
variable [CharZero ğ”½]

def Set_1b : Set (Fin 3 â†’ ğ”½) := {v | (v 0) + (2 * v 1) + (3 * v 2) = 4}

theorem axler_1C_1b :
    Â¬âˆƒ S : Submodule ğ”½ (Fin 3 â†’ ğ”½), (S : Set (Fin 3 â†’ ğ”½)) = Set_1b := by

  -- "Â¬" makes the goal False
  rintro âŸ¨S, hSâŸ©

  -- "zero_mem" proves that the zero vector must be in any submodule,
  -- therefore it's in this one as well.
  have h_0vec_in_set : (0 : Fin 3 â†’ ğ”½) âˆˆ (S : Set _) := S.zero_mem

  -- Replace the symbol "Set_1b" with it's definition.
  unfold Set_1b at *

  --Replace (h_0vec_in_set : 0 âˆˆ â†‘S) with the details of the definition of set S.
  rw [hS, Set.mem_setOf_eq] at h_0vec_in_set
  -- Now (h_0vec_in_set : (0 0) + (2 * 0 1) + (3 * 0 2) = 4)

  -- Apply the zero function (0 i = 0)
  simp only [Pi.zero_apply] at h_0vec_in_set
  -- Now (h_0vec_in_set : 0 + (2 * 0) + (3 * 0) = 4)

  -- a * 0 = 0
  simp only [mul_zero] at h_0vec_in_set
  -- Now (h_0vec_in_set : 0 + 0 + 0 = 4)

  -- a + 0 = a
  simp only [add_zero] at h_0vec_in_set
  -- Now (h_0vec_in_set : 0 = 4)

  -- "norm_num" can prove that "0 = 4" is False in characteristic 0
  norm_num at h_0vec_in_set

end CharZeroExercises
